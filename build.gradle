plugins {
    id "org.jetbrains.intellij" version "0.4.9"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

intellij {
    version '2018.1.8'

    // Specifying these custom repositories enables us to cache them within TIOBE.
    // If this causes trouble, either make sure you are in the TIOBE network, or
    // comment out these lines temporarily.
    intellijRepo 'https://artifacts.tiobe.com/repository/maven/'
    pluginsRepo 'https://artifacts.tiobe.com/repository/maven/'
    jreRepo 'https://artifacts.tiobe.com/repository/intellij-jdk/'
}

patchPluginXml {
    if (!project.hasProperty('SVNVERSION')) {
        ext.SVNVERSION = '?'
    }
    sinceBuild = '172.4155'
    untilBuild = '201.*'
    version "${new File('../../make/TICSVERSION').text.trim()}.${SVNVERSION}"
}

repositories {
    // Specifying this custom repository enables us to cache external libraries within TIOBE.
    // If this causes trouble, either make sure you are in the TIOBE network, or
    // add the repository mavenCentral().
    maven {
        url 'https://artifacts.tiobe.com/repository/maven/'
    }
    //mavenCentral()
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task configureWrapper {
    // Make sure that when upgrading the Gradle wrapper, we still use the local
    // TIOBE mirror.
    doLast { 
        wrapper.distributionUrl = "https://artifacts.tiobe.com/repository/gradle-distributions/gradle-${wrapper.gradleVersion}-bin.zip"
    } 
}   

wrapper.dependsOn configureWrapper
