plugins {
    id "org.jetbrains.intellij" version "1.3.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

intellij {
    version.set('2018.1.8')
    instrumentCode.set(false) // can be removed when targeting IntelliJ 2018.3+

    // Specifying these custom repositories enables us to cache them within TIOBE.
    // If this causes trouble, either make sure you are in the TIOBE network, or
    // comment out these lines temporarily.
    intellijRepository.set('https://artifacts.tiobe.com/repository/maven/')
    jreRepository.set('https://artifacts.tiobe.com/repository/intellij-jdk/')
    pluginsRepositories {
        maven('https://artifacts.tiobe.com/repository/maven/')
    }
}

patchPluginXml {
    if (!project.hasProperty('SVNVERSION')) {
        ext.SVNVERSION = '?'
    }
    sinceBuild = '172.4155'
    untilBuild = '212.*'
    version "${new File('../../make/TICSVERSION').text.trim()}.${SVNVERSION}"
}

repositories {
    // Specifying this custom repository enables us to cache external libraries within TIOBE.
    // If this causes trouble, either make sure you are in the TIOBE network, or
    // add the repository mavenCentral().
    maven {
        url 'https://artifacts.tiobe.com/repository/maven/'
    }
    //mavenCentral()
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.required.set(true)
    }
}

task configureWrapper {
    // Make sure that when upgrading the Gradle wrapper, we still use the local
    // TIOBE mirror.
    doLast { 
        wrapper.distributionUrl = "https://artifacts.tiobe.com/repository/gradle-distributions/gradle-${wrapper.gradleVersion}-bin.zip"
    } 
}   

wrapper.dependsOn configureWrapper
